// A test of Figure 4 (modified) from [Lukose and Mineau 1998].
// The actor graph was drawn using CharGer. Arc ordering is not 
// important here. This example also shows how pCG's run-time 
// type testing can be put to good use.
//
// Note: This shows explicit actor invocation but pCG also supports
//       implicit actor invocation.

// ** Constants **
DBL_QUOTE = 34;

// ** Function definitions **
function mul(x,y,z)
  operand1 = x.designator;
  operand2 = y.designator;
  if not (operand1 is number) or not (operand2 is number) then 
    exit "Operand to " + me.name + " not a number!"; 
  end
  z.designator = operand1 * operand2;
end

function sub1(x,y)
  operand = x.designator;
  if not (operand is number) then 
    exit "Operand to " + me.name + " not a number!"; 
  end
  y.designator = operand - 1;
end

function add1(x,y)
  operand = x.designator;
  if not (operand is number) then 
    exit "Operand to " + me.name + " not a number!"; 
  end
  y.designator = operand + 1;
end

function identityIfEqZero(x,y)
  operand = x.designator;
  if not (operand is number) then 
    exit "Operand to " + me.name + " not a number!"; 
  end
  if operand == 0 then 
    y.designator = operand;
  end
end

function identityIfGTZero(x,y)
  operand = x.designator;
  if not (operand is number) then 
    exit "Operand to " + me.name + " not a number!"; 
  end
  if operand > 0 then 
    y.designator = operand;
  end
end

function quoteNumbersForCharGer(g)
  # Quote numeric literal referents in a graph for CharGer
  # otherwise numbers won't be displayed for some reason.
  # These will later have to be changed to single quotes.
  foreach con in g.concepts do
    value = con.designator;
    if value is number then con.designator = "" + value; end
  end
  return g;
end

// ** Main Program **

// Define actor.
r = file "Factorial.CGF";
actor Factorial(n) is r.readGraph();

println "";
println "** Defining graph for " + Factorial + " is:";
println (Factorial.defgraph).nocomments();

// Invoke actor to compute factorial n.
n = 7; // try making this a string
println "";
g = Factorial(n);
println "** Resulting graph for " + "Factorial(" + n + ") is:";
println g.nocomments();

println "";
println "Note: the above graph shows the top-level recursive invocation";
println "      after unwinding. To see other invocations, add 'option TRACE'";
println "      to the top of this program.";

// Write resulting graph to a file.
g = quoteNumbersForCharGer(g);
out_path = "FactorialOut.cgf";
w = file (">" + out_path);
w.writeln((g + "").replace((DBL_QUOTE).chr(), "'"));
w.close();

println "";
println "See " + out_path + " for CharGer-readable result graph.";
println "";
